<?xml version="1.0" encoding="utf-8"?>
<mx:Box xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx"  visible="false" includeInLayout="false" >
	
	<fx:Metadata>
		[Event(name="load", type="event.ServiceEvent")]
        [Event(name="save", type="event.ServiceEvent")]
		[Event(name="failed", type="event.ServiceEvent")]
	</fx:Metadata>
	
	<fx:Script><![CDATA[
		
		import flash.net.FileFilter;
		import flash.net.URLLoader;
		import flash.net.URLRequest;
		import flash.events.Event;
		import flash.events.IOErrorEvent;
		import mx.rpc.events.ResultEvent;
		import mx.rpc.events.FaultEvent;
		//import mx.core.Application;
		import mx.core.FlexGlobals;
		import event.ServiceEvent;
		import flash.net.FileReference;
		import flash.utils.ByteArray;
		
		private var loader:URLLoader;
		
		private var fileRef:FileReference;
		
		[Bindable]
		private var postURL:String;
		
		//Load
		public function load( url:String ):void 
		{
			loader = new URLLoader( new URLRequest( url ) );
			loader.addEventListener( Event.COMPLETE , onLoadResult);
			loader.addEventListener( IOErrorEvent.IO_ERROR , onLoadFault );
		}
		
		public function loadFile():void 
		{
			fileRef = new FileReference();
			fileRef.addEventListener( Event.SELECT, onFileSelect );
			fileRef.browse();
		}
			
		//Save
		public function save( title:String , svg:XML ):void 
		{
			//postURL = Application.application.approot + EditorConstants.DATA_POST_PATH;
			postURL = FlexGlobals.topLevelApplication.approot + EditorConstants.DATA_POST_PATH;
			saveSvc.send( { "data[title]": title , "data[xml]": svg } );
		}
		
		public function showLoadDialog():void { }
		public function showSaveDialog( xml:XML ):void { }
		
		//Load Result
		private function onLoadResult( e:Event ):void
		{
			var evt:ServiceEvent = new ServiceEvent( ServiceEvent.LOAD );
			evt.result = loader.data;
			dispatchEvent( evt );
		}
		
		private function onLoadFault( e:IOErrorEvent ):void
		{
			var evt:ServiceEvent = new ServiceEvent( ServiceEvent.FAILED );
			evt.message = e.text;
			dispatchEvent( evt );
		}
		
		//Save Result
		private function onSaveResult( e:ResultEvent ):void
		{
			var evt:ServiceEvent = new ServiceEvent( ServiceEvent.SAVE );
			evt.result = e.result;
			dispatchEvent( evt );
		}
		
		private function onFault( e:FaultEvent ):void
		{
			var evt:ServiceEvent = new ServiceEvent( ServiceEvent.FAILED );
			evt.message = e.message.toString();
			dispatchEvent( evt );
		}
		
		//File refference
		private function onFileSelect( e:Event ):void
		{
			fileRef.removeEventListener( Event.SELECT, onFileSelect );
			fileRef.addEventListener(Event.COMPLETE , onFileOpen);
			fileRef.addEventListener(IOErrorEvent.IO_ERROR, onLoadFault);
			fileRef.load();
		}
		
		private function onFileOpen( e:Event ):void
		{
			fileRef.removeEventListener(Event.COMPLETE, onFileOpen);
			fileRef.removeEventListener(IOErrorEvent.IO_ERROR, onLoadFault);
			
			var evt:ServiceEvent = new ServiceEvent( ServiceEvent.LOAD );
			evt.result = fileRef.data;
			dispatchEvent( evt );
		}
		
		
	]]></fx:Script>
	
	<fx:Declarations>
		<mx:HTTPService id="saveSvc" url="{postURL}" method="POST" resultFormat="text" result="onSaveResult( event )" fault="onFault( event ) " />
	</fx:Declarations>
	
	

</mx:Box>